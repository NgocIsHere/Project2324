USE PHONGKHAM_DB2
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHNHASI 
AS
BEGIN
	SELECT ID, HOTEN, NGAYSINH, DIACHI, SDT, GIOITINH FROM dbo.NHASI
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHNHANVIEN 
AS 
BEGIN
	SELECT ID, HOTEN, NGAYSINH, DIACHI, SDT, GIOITINH FROM dbo.NHANVIEN
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHTHUOC 
AS 
BEGIN 
	SELECT ID_THUOC, TENTHUOC, DONGIA, DONVI, SOLUONG, HANSUDUNG, GHICHU FROM dbo.THUOC
END 
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHBENHAN
AS
BEGIN
	SELECT * FROM dbo.BENHAN
END
GO

-- THÊM THUỐC: QTV
CREATE OR ALTER
PROCEDURE SP_THEMTHUOC 
	@TENTHUOC NVARCHAR(255),
    @DONGIA INT,
    @DONVI NVARCHAR(50),
	@SOLUONG INT,
	@HANSUDUNG DATE,
	@GHICHU NVARCHAR(255)
AS
BEGIN
	IF EXISTS (SELECT 1 FROM dbo.THUOC WHERE @TENTHUOC = TENTHUOC)
	BEGIN
		PRINT(N'THUỐC NÀY ĐÃ TỒN TẠI')
		RETURN -1
	END
	
	DECLARE @NewID INT
	SELECT @NewID = COUNT(*) + 2 FROM THUOC
	DECLARE @ID_THUOC VARCHAR(8)
	SET @ID_THUOC = 'TH' + RIGHT('0000000' + CAST(@NewID AS VARCHAR), 6)

	PRINT(@ID_THUOC)

	INSERT INTO dbo.THUOC
	(
	    ID_THUOC,
	    TENTHUOC,
	    DONGIA,
	    DONVI,
	    SOLUONG,
	    HANSUDUNG,
	    GHICHU
	)
	VALUES
	(   @ID_THUOC,   -- ID_THUOC - varchar(8)
	    @TENTHUOC, -- TENTHUOC - nvarchar(255)
	    @DONGIA, -- DONGIA - int
	    @DONVI, -- DONVI - nvarchar(50)
	    @SOLUONG, -- SOLUONG - int
	    @HANSUDUNG, -- HANSUDUNG - date
	    @GHICHU  -- GHICHU - nvarchar(255)
	 )
	 PRINT (N'THÊM THUỐC MỚI THÀNH CÔNG')
	 RETURN 0
END
GO

--CẬP NHẬT THUỐC
CREATE OR ALTER
PROCEDURE SP_CAPNHATTHUOC 
	@ID_THUOC VARCHAR(8),
	@TENTHUOC NVARCHAR(255),
    @DONGIA INT,
    @DONVI NVARCHAR(50),
	@SOLUONG INT,
	@HANSUDUNG DATE,
	@GHICHU NVARCHAR(255)
AS
BEGIN
	-- Kiểm tra thuốc có tồn tại
	IF EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN
		IF (@TENTHUOC = '')
		BEGIN 
			PRINT (N'TÊN THUỐC KHÔNG ĐƯỢC BỎ TRỐNG')
			RETURN -1
		END
		IF (@TENTHUOC IS NULL)
		BEGIN 
			PRINT (N'TÊN THUỐC KHÔNG ĐƯỢC BỎ TRỐNG')
			RETURN -1
		END
		IF (@DONGIA <= 0)
		BEGIN
			PRINT (N'ĐƠN GIÁ KHÔNG HỢP LỆ')
			RETURN -2
		END
		ELSE IF (@SOLUONG < 0)
		BEGIN
			PRINT (N'SỐ LƯỢNG KHÔNG ĐƯỢC NHỎ HƠN 0')
			RETURN -3
		END

		-- Nếu thuốc cập nhật có tên trùng với tên của một loại thuốc khác id
		IF EXISTS (SELECT 1 FROM THUOC WHERE @TENTHUOC = TENTHUOC AND @ID_THUOC <> ID_THUOC)
		BEGIN
			PRINT (N'TRÙNG TÊN THUỐC ĐÃ CÓ TRONG BẢNG')
			RETURN -4
		END

		UPDATE dbo.THUOC 
		SET TENTHUOC = @TENTHUOC, DONGIA = @DONGIA, DONVI = @DONVI, SOLUONG = @SOLUONG, HANSUDUNG = @HANSUDUNG, GHICHU = @GHICHU
		WHERE ID_THUOC = @ID_THUOC
		PRINT (N'CẬP NHẬT THÀNH CÔNG')
		RETURN 0

	END
	PRINT (N'KHÔNG TỒN TẠI THUỐC')
	RETURN -5
END
GO

-- XÓA THUỐC: QTV
CREATE OR ALTER
PROCEDURE SP_XOATHUOC @ID_THUOC VARCHAR(8)
AS
BEGIN
	-- Nếu thuốc không tồn tại 
	IF NOT EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN 
		PRINT (N'KHÔNG TỒN TẠI THUỐC')
		RETURN -1
	END
	-- Nếu tồn tại thuốc muốn xóa trong CHITIETDON
	ELSE IF EXISTS (SELECT 1 FROM dbo.THUOC JOIN dbo.CHITIETDON ON CHITIETDON.ID_THUOC = THUOC.ID_THUOC WHERE THUOC.ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'TỒN TẠI THUỐC TRONG ĐƠN THUỐC. KHÔNG THỂ XÓA')
		RETURN -2
	END
	DELETE FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC
	PRINT (N'XÓA THUỐC THÀNH CÔNG')
	RETURN 0
END
GO

-- THÊM ĐƠN THUỐC
CREATE OR ALTER PROCEDURE SP_THEMDONTHUOC
    @ID_BENHAN CHAR(8),
    @NGAYLAPDON DATE,
    @NewIDDon VARCHAR(8) OUTPUT -- Define an output parameter to hold the new ID_DON
AS
BEGIN
    IF NOT EXISTS (SELECT 1 FROM dbo.BENHAN WHERE @ID_BENHAN = ID)
    BEGIN
        PRINT(N'KHÔNG TỒN TẠI BỆNH NHÂN NÀY')
        RETURN -1
    END

    -- TẠO ID ĐƠN MỚI
    DECLARE @NextID INT
    DECLARE @NewID CHAR(8)

    SELECT @NextID = ISNULL(MAX(CONVERT(INT, RIGHT(ID_DON, 6))), 0) + 1
    FROM DONTHUOC 
    WHERE ID_BENHAN = @ID_BENHAN

    SET @NewID = 'DT' + RIGHT('000000' + CAST(@NextID AS VARCHAR), 6)

    INSERT dbo.DONTHUOC
    (
        ID_DON,
        ID_BENHAN,
        NGAYLAPDON
    )
    VALUES
    (
        @NewID,  -- ID_DON - varchar(8)
        @ID_BENHAN,  -- ID_BENHAN - char(8)
        @NGAYLAPDON -- NGAYLAPDON - date
    )

    SET @NewIDDon = @NewID -- Assign the newly generated ID_DON to the output parameter
    PRINT (N'TẠO ĐƠN THUỐC THÀNH CÔNG')
    RETURN 0
END
GO

-- CẬP NHẬT ĐƠN THUỐC
CREATE OR ALTER
PROCEDURE SP_CAPNHATDONTHUOC
	@ID_DON VARCHAR(8),
	@ID_BENHAN CHAR(8),
	@NGAYLAPDON DATE
AS
BEGIN
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT(N'KHÔNG TỒN TẠI ĐƠN')
		RETURN -1
	END

	UPDATE dbo.DONTHUOC 
	SET NGAYLAPDON = @NGAYLAPDON
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	PRINT (N'CẬP NHẬT ĐƠN THUỐC THÀNH CÔNG')
	RETURN 0
END
GO

-- XÓA ĐƠN THUỐC: VIỆC NÀY SẼ XÓA TOÀN BỘ CHI TIẾT THUỐC VÀ HOÀN LẠI THUỐC VÀO KHO
CREATE OR ALTER PROCEDURE SP_XOADONTHUOC
	@ID_DON VARCHAR(8),
	@ID_BENHAN CHAR(8)
AS
BEGIN
	-- Kiểm tra xem đơn thuốc có tồn tại hay không
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI ĐƠN THUỐC')
		RETURN -1
	END

	BEGIN TRANSACTION

	DECLARE @ID_THUOC VARCHAR(8)
	DECLARE @SOLUONGXUAT INT

	-- Lấy danh sách các thuốc trong đơn thuốc cần xóa
	DECLARE cur CURSOR FOR
	SELECT ID_THUOC, SOLUONG
	FROM dbo.CHITIETDON
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	OPEN cur

	FETCH NEXT FROM cur INTO @ID_THUOC, @SOLUONGXUAT

	WHILE @@FETCH_STATUS = 0
	BEGIN
		-- Cập nhật lại số lượng thuốc trong kho
		UPDATE dbo.THUOC
		SET SOLUONG = SOLUONG + @SOLUONGXUAT
		WHERE ID_THUOC = @ID_THUOC

		FETCH NEXT FROM cur INTO @ID_THUOC, @SOLUONGXUAT
	END

	CLOSE cur
	DEALLOCATE cur

	-- Xóa toàn bộ thông tin về đơn thuốc
	DELETE FROM dbo.CHITIETDON 
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	DELETE FROM dbo.DONTHUOC
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	COMMIT TRANSACTION

	PRINT (N'ĐÃ XÓA ĐƠN THUỐC VÀ TRẢ LẠI SỐ LƯỢNG THUỐC VỀ KHO')
	RETURN 0
END
GO

-- THÊM THUỐC VÀO CHI TIẾT ĐƠN
CREATE OR ALTER
PROCEDURE SP_THEMCHITIETDON
	@ID_DON VARCHAR(8),
	@ID_BENHAN CHAR(8),
	@ID_THUOC VARCHAR(8),
	@SOLUONG INT
AS 
BEGIN
	-- KHÔNG TỒN TẠI ĐƠN THUỐC
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI ĐƠN THUỐC')
		RETURN -1
	END
	-- KHÔNG TỒN TẠI THUỐC
	IF NOT EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI THUỐC')
		RETURN -2
	END
	-- ĐÃ TỒN TẠI THUỐC TRONG ĐƠN, KHÔNG THỂ THÊM
	IF EXISTS (SELECT 1 FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'ĐÃ TỒN TẠI THUỐC TRONG ĐƠN, KHÔNG THỂ THÊM')
		RETURN -3
	END
	-- SỐ LƯỢNG XUẤT LỚN HƠN SỐ LƯỢNG TỒN KHO
	DECLARE @SOLUONGTONKHO INT
	SELECT @SOLUONGTONKHO = SOLUONG FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC
	IF (@SOLUONG > @SOLUONGTONKHO)
	BEGIN
		PRINT (N'SỐ LƯỢNG KHÔNG ĐỦ')
		RETURN -4
	END
	-- SỐ LƯỢNG XUẤT NHỎ HƠN 0
	IF (@SOLUONG <= 0 OR @SOLUONG IS NULL)
	BEGIN
		PRINT (N'SỐ LƯỢNG XUẤT KHÔNG HỢP LỆ')
		RETURN -5
	END

	BEGIN TRANSACTION

	-- THÊM THUỐC VÀO ĐƠN
	INSERT INTO dbo.CHITIETDON
	(
	    ID_DON,
	    ID_BENHAN,
	    ID_THUOC,
	    SOLUONG
	)
	VALUES
	(   @ID_DON,  -- ID_DON - varchar(8)
	    @ID_BENHAN,  -- ID_BENHAN - char(8)
	    @ID_THUOC,  -- ID_THUOC - varchar(8)
	    @SOLUONG -- SOLUONG - int
	)

	-- TRỪ SỐ LƯỢNG THUỐC RA KHỎI BẢNG THUỐC
	UPDATE dbo.THUOC
	SET SOLUONG = SOLUONG - @SOLUONG
	WHERE ID_THUOC = @ID_THUOC

	COMMIT TRANSACTION

	PRINT (N'ĐÃ THÊM THUỐC VÀO ĐƠN THÀNH CÔNG')
	RETURN 0
END
GO

-- XÓA THUỐC RA KHỎI CHI TIẾT ĐƠN
CREATE OR ALTER
PROCEDURE SP_XOACHITIETDON
	@ID_DON VARCHAR(8),
	@ID_BENHAN CHAR(8),
	@ID_THUOC VARCHAR(8)
AS
BEGIN
	--KHÔNG TỒN TẠI CHI TIẾT ĐƠN
	IF NOT EXISTS (SELECT 1 FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI CHI TIẾT ĐƠN')
		RETURN -1
	END

	BEGIN TRANSACTION

	DECLARE @SOLUONGXUAT INT
	SELECT @SOLUONGXUAT = SOLUONG FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC

	-- CẬP NHẬT LẠI SỐ LƯỢNG	
	UPDATE dbo.THUOC
	SET SOLUONG = SOLUONG + @SOLUONGXUAT
	WHERE ID_THUOC = @ID_THUOC

	-- XÓA THUỐC RA KHỎI ĐƠN
	DELETE FROM dbo.CHITIETDON 
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC

	COMMIT TRANSACTION
	PRINT (N'XÓA THUỐC RA KHỎI ĐƠN THÀNH CÔNG')
	RETURN 0
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMCHITIETDON @ID_DON VARCHAR(8), @ID_BENHAN CHAR(8)
AS
BEGIN
	SELECT dbo.CHITIETDON.ID_THUOC, dbo.THUOC.TENTHUOC, dbo.CHITIETDON.SOLUONG
	FROM dbo.CHITIETDON JOIN dbo.THUOC ON THUOC.ID_THUOC = CHITIETDON.ID_THUOC
	WHERE dbo.CHITIETDON.ID_DON = @ID_DON AND dbo.CHITIETDON.ID_BENHAN = @ID_BENHAN
END
GO

------------------------------------------------------------------------------------------

EXEC dbo.SP_ThemThuocVaoDon @ID_DON = 'DO000004',    -- varchar(8)
                            @ID_BENHAN = 'BN000001', -- char(8)
                            @ID_THUOC = 'TH000001',  -- varchar(8)
                            @SoLuong = 30     -- int

SELECT * FROM dbo.THUOC

EXEC dbo.SP_CAPNHATTHUOC @ID_THUOC = 'TH000006',            -- varchar(8)
                         @TENTHUOC = null,           -- nvarchar(255)
                         @DONGIA = -1,               -- int
                         @DONVI = N'Viên',              -- nvarchar(50)
                         @SOLUONG = 100,              -- int
                         @HANSUDUNG = '2023-12-22', -- date
                         @GHICHU = N'Thuốc giảm đau'              -- nvarchar(255)

SELECT CHITIETDON.ID_DON, CHITIETDON.ID_BENHAN, THUOC.ID_THUOC, TENTHUOC, CHITIETDON.SOLUONG 
FROM dbo.CHITIETDON JOIN dbo.DONTHUOC ON DONTHUOC.ID_DON = CHITIETDON.ID_DON AND DONTHUOC.ID_BENHAN = CHITIETDON.ID_BENHAN 
JOIN dbo.THUOC ON THUOC.ID_THUOC = CHITIETDON.ID_THUOC

EXEC dbo.SP_XOATHUOC @ID_THUOC = 'TH000004' -- varchar(8)

SELECT * FROM dbo.DONTHUOC

EXEC dbo.SP_THEMDONTHUOC @ID_BENHAN = 'BN000002',           -- char(8)
                         @NGAYLAPDON = '2023-12-22' -- date

EXEC dbo.SP_CAPNHATDONTHUOC @ID_DON = 'DT000005',              -- varchar(8)
                            @ID_BENHAN = 'BN000001',           -- char(8)
                            @NGAYLAPDON = '2023-12-22' -- date

SELECT * FROM dbo.CHITIETDON
EXEC dbo.SP_THEMCHITIETDON @ID_DON = 'DT000001',    -- varchar(8)
                           @ID_BENHAN = 'BN000002', -- char(8)
                           @ID_THUOC = 'TH000001',  -- varchar(8)
                           @SOLUONG = 0     -- int

EXEC dbo.SP_XOACHITIETDON @ID_DON = 'DT000001',    -- varchar(8)
                          @ID_BENHAN = 'BN000001', -- char(8)
                          @ID_THUOC = 'TH000001'   -- varchar(8)

EXEC dbo.SP_XOADONTHUOC @ID_DON = 'DT000001',   -- varchar(8)
                        @ID_BENHAN = 'BN000002' -- char(8)

EXEC dbo.SP_THEMTHUOC @TENTHUOC = N'Nicotin',           -- nvarchar(255)
                      @DONGIA = 20000,               -- int
                      @DONVI = N'Viên',              -- nvarchar(50)
                      @SOLUONG = 100,              -- int
                      @HANSUDUNG = '2023-12-24', -- date
                      @GHICHU = N'Thuốc có hại'              -- nvarchar(255)


SELECT * FROM dbo.BENHAN