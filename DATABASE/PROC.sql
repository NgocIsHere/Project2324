USE PHONGKHAM_DB2
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHNHASI 
AS
BEGIN
	SELECT ID, HOTEN, NGAYSINH, DIACHI, SDT, GIOITINH FROM dbo.NHASI
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHNHANVIEN 
AS 
BEGIN
	SELECT ID, HOTEN, NGAYSINH, DIACHI, SDT, GIOITINH FROM dbo.NHANVIEN
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMDSTHUOC 
AS 
BEGIN 
	SELECT * FROM dbo.THUOC
END 
GO

CREATE OR ALTER
PROCEDURE SP_XEMDANHSACHBENHAN
AS
BEGIN
	SELECT * FROM dbo.BENHAN
END
GO

CREATE OR ALTER
PROCEDURE SP_XemDonThuocCuaBenhNhan
	@ID_BENHAN INT
AS
BEGIN 
	IF NOT EXISTS (SELECT 1 FROM dbo.BENHAN WHERE @ID_BENHAN = ID)
	BEGIN
		PRINT(N'BỆNH NHÂN KHÔNG TỒN TẠI')
		RETURN -1
	END
	SELECT ID_DON, ID_BENHAN, HOTEN, NGAYLAPDON FROM dbo.DONTHUOC JOIN dbo.BENHAN ON BENHAN.ID = DONTHUOC.ID_BENHAN
	WHERE @ID_BENHAN = ID_BENHAN
	RETURN 0
END
GO


-- THÊM THUỐC: QTV
CREATE OR ALTER PROCEDURE SP_THEMTHUOC 
	@TENTHUOC NVARCHAR(60),
    @DONGIA INT,
    @DONVI NVARCHAR(30),
	@SOLUONG INT,
	@HANSUDUNG DATE,
	@GHICHU NVARCHAR(255)
AS
BEGIN
	IF EXISTS (SELECT 1 FROM dbo.THUOC WHERE @TENTHUOC = TENTHUOC)
	BEGIN
		RETURN -1
	END
	-- Insert the new record with the calculated ID_THUOC
	INSERT INTO dbo.THUOC
	(
	    TENTHUOC,
	    DONGIA,
	    DONVI,
	    SOLUONG,
	    HANSUDUNG,
	    GHICHU
	)
	VALUES
	(   
	    @TENTHUOC,
	    @DONGIA, 
	    @DONVI, 
	    @SOLUONG,
	    @HANSUDUNG,
	    @GHICHU 
	 )
	 PRINT (N'THÊM THUỐC MỚI THÀNH CÔNG')
	 RETURN 0
END
GO

--CẬP NHẬT THUỐC
CREATE OR ALTER
PROCEDURE SP_CAPNHATTHUOC 
	@ID_THUOC INT,
	@TENTHUOC NVARCHAR(60),
    @DONGIA INT,
    @DONVI NVARCHAR(30),
	@SOLUONG INT,
	@HANSUDUNG DATE,
	@GHICHU NVARCHAR(255)
AS
BEGIN
	-- Kiểm tra thuốc có tồn tại
	IF EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN
		IF (@TENTHUOC = '')
		BEGIN 
			PRINT (N'TÊN THUỐC KHÔNG ĐƯỢC BỎ TRỐNG')
			RETURN -1
		END
		IF (@TENTHUOC IS NULL)
		BEGIN 
			PRINT (N'TÊN THUỐC KHÔNG ĐƯỢC BỎ TRỐNG')
			RETURN -1
		END
		IF (@DONGIA <= 0)
		BEGIN
			PRINT (N'ĐƠN GIÁ KHÔNG HỢP LỆ')
			RETURN -2
		END
		ELSE IF (@SOLUONG < 0)
		BEGIN
			PRINT (N'SỐ LƯỢNG KHÔNG ĐƯỢC NHỎ HƠN 0')
			RETURN -3
		END

		-- Nếu thuốc cập nhật có tên trùng với tên của một loại thuốc khác id
		IF EXISTS (SELECT 1 FROM THUOC WHERE @TENTHUOC = TENTHUOC AND @ID_THUOC <> ID_THUOC)
		BEGIN
			PRINT (N'TRÙNG TÊN THUỐC ĐÃ CÓ TRONG BẢNG')
			RETURN -4
		END

		UPDATE dbo.THUOC 
		SET TENTHUOC = @TENTHUOC, DONGIA = @DONGIA, DONVI = @DONVI, SOLUONG = @SOLUONG, HANSUDUNG = @HANSUDUNG, GHICHU = @GHICHU
		WHERE ID_THUOC = @ID_THUOC
		PRINT (N'CẬP NHẬT THÀNH CÔNG')
		RETURN 0

	END
	PRINT (N'KHÔNG TỒN TẠI THUỐC')
	RETURN -5
END
GO

-- XÓA THUỐC: QTV
CREATE OR ALTER
PROCEDURE SP_XOATHUOC @ID_THUOC INT
AS
BEGIN
	-- Nếu thuốc không tồn tại 
	IF NOT EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN 
		PRINT (N'KHÔNG TỒN TẠI THUỐC')
		RETURN -1
	END
	-- Nếu tồn tại thuốc muốn xóa trong CHITIETDON
	ELSE IF EXISTS (SELECT 1 FROM dbo.THUOC JOIN dbo.CHITIETDON ON CHITIETDON.ID_THUOC = THUOC.ID_THUOC WHERE THUOC.ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'TỒN TẠI THUỐC TRONG ĐƠN THUỐC. KHÔNG THỂ XÓA')
		RETURN -2
	END
	DELETE FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC
	PRINT (N'XÓA THUỐC THÀNH CÔNG')
	RETURN 0
END
GO

-- THÊM ĐƠN THUỐC
CREATE OR ALTER PROCEDURE SP_THEMDONTHUOC
    @ID_BENHAN INT,
    @NGAYLAPDON DATE,
    @ID_DON INT OUTPUT
AS
BEGIN
    IF NOT EXISTS (SELECT 1 FROM dbo.BENHAN WHERE @ID_BENHAN = ID)
    BEGIN
        PRINT(N'KHÔNG TỒN TẠI BỆNH NHÂN NÀY')
        RETURN -1
    END

    INSERT INTO dbo.DONTHUOC (ID_BENHAN, NGAYLAPDON)
    VALUES (@ID_BENHAN, @NGAYLAPDON)

	SET @ID_DON = SCOPE_IDENTITY()
    PRINT (N'TẠO ĐƠN THUỐC THÀNH CÔNG')
    RETURN 0
END
GO

-- CẬP NHẬT ĐƠN THUỐC
CREATE OR ALTER
PROCEDURE SP_CAPNHATDONTHUOC
	@ID_DON INT,
	@ID_BENHAN INT,
	@NGAYLAPDON DATE
AS
BEGIN
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT(N'KHÔNG TỒN TẠI ĐƠN')
		RETURN -1
	END

	UPDATE dbo.DONTHUOC 
	SET NGAYLAPDON = @NGAYLAPDON
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	PRINT (N'CẬP NHẬT ĐƠN THUỐC THÀNH CÔNG')
	RETURN 0
END
GO

-- XÓA ĐƠN THUỐC: VIỆC NÀY SẼ XÓA TOÀN BỘ CHI TIẾT THUỐC VÀ HOÀN LẠI THUỐC VÀO KHO
CREATE OR ALTER PROCEDURE SP_XOADONTHUOC
	@ID_DON INT,
	@ID_BENHAN INT
AS
BEGIN
	-- Kiểm tra xem đơn thuốc có tồn tại hay không
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI')
		RETURN -1
	END

	BEGIN TRANSACTION

	DECLARE @ID_THUOC INT
	DECLARE @SOLUONGXUAT INT

	-- Lấy danh sách các thuốc trong đơn thuốc cần xóa
	DECLARE cur CURSOR FOR
	SELECT ID_THUOC, SOLUONG
	FROM dbo.CHITIETDON
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	OPEN cur

	FETCH NEXT FROM cur INTO @ID_THUOC, @SOLUONGXUAT

	WHILE @@FETCH_STATUS = 0
	BEGIN
		-- Cập nhật lại số lượng thuốc trong kho
		UPDATE dbo.THUOC
		SET SOLUONG = SOLUONG + @SOLUONGXUAT
		WHERE ID_THUOC = @ID_THUOC

		FETCH NEXT FROM cur INTO @ID_THUOC, @SOLUONGXUAT
	END

	CLOSE cur
	DEALLOCATE cur

	-- Xóa toàn bộ thông tin về đơn thuốc
	DELETE FROM dbo.CHITIETDON 
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	DELETE FROM dbo.DONTHUOC
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN

	COMMIT TRANSACTION

	PRINT (N'ĐÃ XÓA ĐƠN THUỐC VÀ TRẢ LẠI SỐ LƯỢNG THUỐC VỀ KHO')
	RETURN 0
END
GO

-- THÊM THUỐC VÀO CHI TIẾT ĐƠN
CREATE OR ALTER
PROCEDURE SP_THEMCHITIETDON
	@ID_DON INT,
	@ID_BENHAN INT,
	@ID_THUOC INT,
	@SOLUONG INT,
	@GHICHU NVARCHAR(30)
AS 
BEGIN
	-- KHÔNG TỒN TẠI ĐƠN THUỐC
	IF NOT EXISTS (SELECT 1 FROM dbo.DONTHUOC WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI ĐƠN THUỐC')
		RETURN -1
	END
	-- KHÔNG TỒN TẠI THUỐC
	IF NOT EXISTS (SELECT 1 FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI THUỐC')
		RETURN -2
	END
	-- ĐÃ TỒN TẠI THUỐC TRONG ĐƠN, KHÔNG THỂ THÊM
	IF EXISTS (SELECT 1 FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'ĐÃ TỒN TẠI THUỐC TRONG ĐƠN, KHÔNG THỂ THÊM')
		RETURN -3
	END
	-- SỐ LƯỢNG XUẤT LỚN HƠN SỐ LƯỢNG TỒN KHO
	DECLARE @SOLUONGTONKHO INT
	SELECT @SOLUONGTONKHO = SOLUONG FROM dbo.THUOC WHERE ID_THUOC = @ID_THUOC
	IF (@SOLUONG > @SOLUONGTONKHO)
	BEGIN
		PRINT (N'SỐ LƯỢNG KHÔNG ĐỦ')
		RETURN -4
	END
	-- SỐ LƯỢNG XUẤT NHỎ HƠN 0
	IF (@SOLUONG <= 0 OR @SOLUONG IS NULL)
	BEGIN
		PRINT (N'SỐ LƯỢNG XUẤT KHÔNG HỢP LỆ')
		RETURN -5
	END

	BEGIN TRANSACTION

	-- THÊM THUỐC VÀO ĐƠN
	INSERT INTO dbo.CHITIETDON
	(
	    ID_DON,
	    ID_BENHAN,
	    ID_THUOC,
	    SOLUONG,
		GHICHU
	)
	VALUES
	(   @ID_DON,  -- ID_DON - varchar(8)
	    @ID_BENHAN,  -- ID_BENHAN - char(8)
	    @ID_THUOC,  -- ID_THUOC - varchar(8)
	    @SOLUONG, -- SOLUONG - int
		@GHICHU
	)

	-- TRỪ SỐ LƯỢNG THUỐC RA KHỎI BẢNG THUỐC
	UPDATE dbo.THUOC
	SET SOLUONG = SOLUONG - @SOLUONG
	WHERE ID_THUOC = @ID_THUOC

	COMMIT TRANSACTION

	PRINT (N'ĐÃ THÊM THUỐC VÀO ĐƠN THÀNH CÔNG')
	RETURN 0
END
GO

-- XÓA THUỐC RA KHỎI CHI TIẾT ĐƠN
CREATE OR ALTER
PROCEDURE SP_XOACHITIETDON
	@ID_DON INT,
	@ID_BENHAN INT,
	@ID_THUOC INT
AS
BEGIN
	--KHÔNG TỒN TẠI CHI TIẾT ĐƠN
	IF NOT EXISTS (SELECT 1 FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC)
	BEGIN
		PRINT (N'KHÔNG TỒN TẠI CHI TIẾT ĐƠN')
		RETURN -1
	END

	BEGIN TRANSACTION

	DECLARE @SOLUONGXUAT INT
	SELECT @SOLUONGXUAT = SOLUONG FROM dbo.CHITIETDON WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC

	-- CẬP NHẬT LẠI SỐ LƯỢNG	
	UPDATE dbo.THUOC
	SET SOLUONG = SOLUONG + @SOLUONGXUAT
	WHERE ID_THUOC = @ID_THUOC

	-- XÓA THUỐC RA KHỎI ĐƠN
	DELETE FROM dbo.CHITIETDON 
	WHERE ID_DON = @ID_DON AND ID_BENHAN = @ID_BENHAN AND ID_THUOC = @ID_THUOC

	COMMIT TRANSACTION
	PRINT (N'XÓA THUỐC RA KHỎI ĐƠN THÀNH CÔNG')
	RETURN 0
END
GO

CREATE OR ALTER
PROCEDURE SP_XEMCHITIETDON @ID_DON INT, @ID_BENHAN INT
AS
BEGIN
	SELECT dbo.CHITIETDON.ID_THUOC, dbo.THUOC.TENTHUOC, dbo.CHITIETDON.SOLUONG, dbo.CHITIETDON.GHICHU
	FROM dbo.CHITIETDON JOIN dbo.THUOC ON THUOC.ID_THUOC = CHITIETDON.ID_THUOC
	WHERE dbo.CHITIETDON.ID_DON = @ID_DON AND dbo.CHITIETDON.ID_BENHAN = @ID_BENHAN
END
GO

------------------------------------------------------------------------------------------

SELECT * FROM dbo.THUOC

EXEC dbo.SP_THEMTHUOC @TENTHUOC = N'TEST THÊM THUỐC',           -- nvarchar(60)
                      @DONGIA = 10000,               -- int
                      @DONVI = N'Viên',              -- nvarchar(30)
                      @SOLUONG = 120,              -- int
                      @HANSUDUNG = '2023-12-31', -- date
                      @GHICHU = N'Test thêm thuốc'              -- nvarchar(255)


EXEC dbo.SP_XOATHUOC @ID_THUOC = 6 -- int

EXEC dbo.SP_CAPNHATTHUOC @ID_THUOC = 7,             -- int
                         @TENTHUOC = N'TEST CẬP NHẬT',           -- nvarchar(60)
                         @DONGIA = 10000,               -- int
                         @DONVI = N'Thùng',              -- nvarchar(30)
                         @SOLUONG = 129,              -- int
                         @HANSUDUNG = '2023-12-31', -- date
                         @GHICHU = N'Cập nhật'              -- nvarchar(255)

DECLARE @ID_DON INT;
EXEC dbo.SP_THEMDONTHUOC @ID_BENHAN = 1,             -- int
                         @NGAYLAPDON = '2023-12-31', -- date
                         @ID_DON = @ID_DON OUTPUT    -- int

PRINT(@ID_DON)

EXEC dbo.SP_XEMDSTHUOC
EXEC dbo.SP_CAPNHATDONTHUOC @ID_DON = 6,               -- int
                            @ID_BENHAN = 1,            -- int
                            @NGAYLAPDON = '2023-12-12' -- date

EXEC dbo.SP_XemDonThuocCuaBenhNhan @ID_BENHAN = 1

EXEC dbo.SP_XOADONTHUOC @ID_DON = 6,   -- int
                        @ID_BENHAN = 1 -- int

EXEC dbo.SP_THEMCHITIETDON @ID_DON = 1,    -- int
                           @ID_BENHAN = 1, -- int
                           @ID_THUOC = 7,  -- int
                           @SOLUONG = 50   -- int

EXEC dbo.SP_XEMCHITIETDON @ID_DON = 1,   -- int
                          @ID_BENHAN = 2 -- int

SELECT * FROM dbo.CHITIETDON

EXEC dbo.SP_XOACHITIETDON @ID_DON = 1,    -- int
                          @ID_BENHAN = 1, -- int
                          @ID_THUOC = 7   -- int

EXEC dbo.SP_XEMDSTHUOC





